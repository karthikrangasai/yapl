PROGRAM -> FUNCTIONS

FUNCTIONS -> FUNCTION FUNCTIONS
FUNCTIONS -> EMPTY

FUNCTION -> FUNTION_KEYWORD RETURN_TYPE BINDING_OPERATOR FUNCTION_IDENTIFIER LEFT_PARENTHESES FUNCTION_PARAMETERS RIGHT_PARENTHESES LEFT_FLOWER_BRACKET COMPOUND_STATEMENTS RIGHT_FLOWER_BRACKET END_DEFINITION_KEYWORD

FUNCTION_PARAMETERS -> PARAMETERS
FUNCTION_PARAMETERS -> VOID_KEYWORD
PARAMETERS -> PARAMETER MORE_PARAMETERS
MORE_PARAMETERS -> COMMA_SEPARATOR PARAMETERS
MORE_PARAMETERS -> EMPTY
PARAMETER -> DATA_TYPE BINDING_OPERATOR IDENTIFIER
PARAMETER -> INTEGER
PARAMETER -> BOOLEAN

COMPOUND_STATEMENTS -> STATEMENT COMPOUND_STATEMENTS
COMPOUND_STATEMENTS -> EMPTY

STATEMENT -> LOOP_STATEMENT
STATEMENT -> CONDITIONAL_STATEMENTS
STATEMENT -> BREAK_STATEMENTS SEMI_COLON_SEPARATOR
STATEMENT -> FUNCTION_CALL SEMI_COLON_SEPARATOR
STATEMENT -> INITIALIZATION_STATEMENTS SEMI_COLON_SEPARATOR

LOOP_STATEMENT -> WHILE_KEYWORD LEFT_PARENTHESES LOGICAL_EXPRESSION RIGHT_PARENTHESES LEFT_FLOWER_BRACKET COMPOUND_STATEMENTS RIGHT_FLOWER_BRACKET END_KEYWORD

CONDITIONAL_STATEMENTS -> IF_KEYWORD LEFT_PARENTHESES LOGICAL_EXPRESSION RIGHT_PARENTHESES LEFT_FLOWER_BRACKET COMPOUND_STATEMENTS RIGHT_FLOWER_BRACKET ELSE_STATEMENT
ELSE_STATEMENT -> ELSE_KEYWORD LEFT_FLOWER_BRACKET COMPOUND_STATEMENTS RIGHT_FLOWER_BRACKET
ELSE_STATEMENT -> EMPTY

BREAK_STATEMENTS -> BREAK_KEYWORD
BREAK_STATEMENTS -> CONTINUE_KEYWORD
BREAK_STATEMENTS -> RETURN_KEYWORD RETURN_ENTITY
RETURN_ENTITY -> EXPRESSION
RETURN_ENTITY -> EMPTY

FUNCTION_CALL -> FUNCTION_IDENTIFIER LEFT_PARENTHESES ARGUEMENTS RIGHT_PARENTHESES

ARGUEMENTS -> VOID_KEYWORD
ARGUEMENTS -> TERMIMAL_EXPRESSION ADDITIONAL_ARGUMENTS
ADDITIONAL_ARGUMENTS -> COMMA_SEPARATOR ARGUEMENTS
ADDITIONAL_ARGUMENTS -> EMPTY

INITIALIZATION_STATEMENTS -> DATA_TYPE BINDING_OPERATOR TERMINAL_ID ASSIGN_STATEMENT
INITIALIZATION_STATEMENTS -> TERMINAL_ID ASSIGNMENT_OPERATOR EXPRESSION
ASSIGN_STATEMENT -> ASSIGNMENT_OPERATOR EXPRESSION
ASSIGN_STATEMENT -> EMPTY

TERMIMAL_EXPRESSIONS -> TERMIMAL_EXPRESSION ADDITIONAL_TERMINALS
ADDITIONAL_TERMINALS -> COMMA_SEPARATOR TERMIMAL_EXPRESSIONS
ADDITIONAL_TERMINALS -> EMPTY

EXPRESSION -> LOGICAL_EXPRESSION

LOGICAL_EXPRESSION -> LOGICAL_BRAKET_EXPRESSION LOGICAL_EXPR_EQUALITY_OPERATION LOGICAL_EXPR_RELATIONAL_OPERATION
LOGICAL_EXPRESSION -> NOT_OPERATOR LOGICAL_EXPRESSION LOGICAL_EXPR_RELATIONAL_OPERATION
LOGICAL_EXPR_EQUALITY_OPERATION -> REL_EQ_OP LOGICAL_EXPRESSION LOGICAL_EXPR_EQUALITY_OPERATION
LOGICAL_EXPR_EQUALITY_OPERATION -> EMPTY
LOGICAL_EXPR_RELATIONAL_OPERATION -> LOGICAL_AND_OR_OP LOGICAL_EXPRESSION LOGICAL_EXPR_RELATIONAL_OPERATION
LOGICAL_EXPR_RELATIONAL_OPERATION -> EMPTY

LOGICAL_AND_OR_OP -> OR_OPERATOR LOGICAL_EXPRESSION
LOGICAL_AND_OR_OP -> AND_OPERATOR LOGICAL_EXPRESSION

LOGICAL_BRAKET_EXPRESSION -> RELATIONAL_EXPRESSION
LOGICAL_BRAKET_EXPRESSION -> LEFT_PARENTHESES LOGICAL_EXPRESSION RIGHT_PARENTHESES

RELATIONAL_EXPRESSION -> LEFT_PARENTHESES RELATIONAL_EXPRESSION RIGHT_PARENTHESES
RELATIONAL_EXPRESSION -> BOOLEAN
RELATIONAL_EXPRESSION -> NUMERIC_EXPRESSION RELATIONAL_OPERATION
RELATIONAL_OPERATION -> REL_OP NUMERIC_EXPRESSION
RELATIONAL_OPERATION -> EMPTY

NUMERIC_EXPRESSION -> MULTIPLICATION_EXPRESSION NUMERIC_OPERATION
NUMERIC_OPERATION -> ADDITION_OPERATOR NUMERIC_EXPRESSION
NUMERIC_OPERATION -> SUBTRACTION_OPERATOR NUMERIC_EXPRESSION
NUMERIC_OPERATION -> EMPTY

MULTIPLICATION_EXPRESSION -> NUMERIC_BRAKET_EXPRESSION MULT_DIV_MOD_OPERATIONS
MULT_DIV_MOD_OPERATIONS -> DIVISION_OPERATOR MULTIPLICATION_EXPRESSION
MULT_DIV_MOD_OPERATIONS -> MULTIPLICATION_OPERATOR MULTIPLICATION_EXPRESSION
MULT_DIV_MOD_OPERATIONS -> MODULO_OPERATOR MULTIPLICATION_EXPRESSION
MULT_DIV_MOD_OPERATIONS -> EMPTY

NUMERIC_BRAKET_EXPRESSION -> INTEGER
NUMERIC_BRAKET_EXPRESSION -> TERMIMAL_EXPRESSION
NUMERIC_BRAKET_EXPRESSION -> LEFT_PARENTHESES NUMERIC_EXPRESSION RIGHT_PARENTHESES

REL_OP -> REL_EQ_OP
REL_OP -> REL_INEQ_OP
REL_EQ_OP -> INEQUALITY_OPERATOR
REL_EQ_OP -> EQAULITY_OPERATOR
REL_INEQ_OP -> GREATER_THAN_EQUAL_OPERATOR
REL_INEQ_OP -> LESS_THAN_EQUAL_OPERATOR
REL_INEQ_OP -> GREATER_THAN_OPERATOR
REL_INEQ_OP -> LESS_THAN_OPERATOR

TERMIMAL_EXPRESSION -> TERMINAL_ID
TERMIMAL_EXPRESSION -> FUNCTION_CALL
TERMINAL_ID -> IDENTIFIER ARRAY_IDENTIFIER

ARRAY_IDENTIFIER -> LEFT_SQUARE_BRACKET INSIDE_ARRAY RIGHT_SQUARE_BRACKET
ARRAY_IDENTIFIER -> EMPTY

INSIDE_ARRAY -> INTEGER
INSIDE_ARRAY -> IDENTIFIER

RETURN_TYPE -> DATA_TYPE
RETURN_TYPE -> VOID_KEYWORD

DATA_TYPE -> BOOLEAN_KEYWORD
DATA_TYPE -> INTEGER_KEYWORD

BOOLEAN -> TRUE_KEYWORD
BOOLEAN -> FALSE_KEYWORD

INTEGER -> POSITIVE_INTEGER
